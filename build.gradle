plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        name 'forge'
        url 'https://maven.minecraftforge.net/'
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

group = 'com.github.mjaroslav.bon2gradle'
version = '0.1.0'
archivesBaseName = "Bon2Gradle"

java {
    withSourcesJar()
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    if (Boolean.parseBoolean(System.getenv("CI")))
        dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    useJUnitPlatform()
}

configure([tasks.compileJava, tasks.compileTestJava, tasks.compileFunctionalTestJava]) {
    sourceCompatibility = 16 // for the IDE support
    options.release = 8
    javaCompiler = javaToolchains.compilerFor { languageVersion = JavaLanguageVersion.of(16) }
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    compileOnly("com.anatawa12.forge:ForgeGradle:${project.forgegradle_version}") {
        changing = true
    }
    runtimeOnly("com.anatawa12.forge:ForgeGradle:${project.forgegradle_version}") {
        changing = true
    }
    implementation "com.github.FIXxp:BON2:${project.bon2_version}:bin"
    testImplementation("com.anatawa12.forge:ForgeGradle:${project.forgegradle_version}") {
        changing = true
    }
    testImplementation "org.junit.jupiter:junit-jupiter:${project.junit_version}"

    compileOnly "org.jetbrains:annotations:${project.jetbrains_annotations_version}"
    testCompileOnly "org.jetbrains:annotations:${project.jetbrains_annotations_version}"
    functionalTestCompileOnly "org.jetbrains:annotations:${project.jetbrains_annotations_version}"
    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    testCompileOnly "org.projectlombok:lombok:${project.lombok_version}"
    testAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    functionalTestCompileOnly "org.projectlombok:lombok:${project.lombok_version}"
    functionalTestAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "com.github.bsideup.jabel:jabel-javac-plugin:${project.jabel_version}"
    testAnnotationProcessor "com.github.bsideup.jabel:jabel-javac-plugin:${project.jabel_version}"
    functionalTestAnnotationProcessor "com.github.bsideup.jabel:jabel-javac-plugin:${project.jabel_version}"
}

gradlePlugin {
    plugins {
        bon2gradle {
            id = 'bon2gradle'
            implementationClass = 'com.github.mjaroslav.bon2gradle.Bon2GradlePlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }
}
